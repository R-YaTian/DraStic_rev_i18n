
<vertex>
attribute vec2 a_vertex_coordinate;
attribute vec2 a_texture_coordinate;
uniform vec4 u_texture_size;

varying vec2 v_texture_coordinate;
varying vec4 v_texture_coordinate_1;
varying vec4 v_texture_coordinate_2;
varying vec4 v_texture_coordinate_3;
varying vec4 v_texture_coordinate_4;
void main()
{
  float d = 0.5 * u_texture_size.x;
  vec2 dg1 = vec2(d, d);
  vec2 dg2 = vec2(-d, d);
  vec2 dx = vec2(d, 0.0);
  vec2 dy = vec2(0.0, d);
  gl_Position = vec4(a_vertex_coordinate.xy, 0.0, 1.0);
  v_texture_coordinate = a_texture_coordinate;
  v_texture_coordinate_1.xy = v_texture_coordinate.xy - dg1;
  v_texture_coordinate_1.zw = v_texture_coordinate.xy - dy;
  v_texture_coordinate_2.xy = v_texture_coordinate.xy - dg2;
  v_texture_coordinate_2.zw = v_texture_coordinate.xy + dx;
  v_texture_coordinate_3.xy = v_texture_coordinate.xy + dg1;
  v_texture_coordinate_3.zw = v_texture_coordinate.xy + dy;
  v_texture_coordinate_4.xy = v_texture_coordinate.xy + dg2;
  v_texture_coordinate_4.zw = v_texture_coordinate.xy - dx;
}
</vertex>

<fragment>
varying vec2 v_texture_coordinate;
varying vec4 v_texture_coordinate_1;
varying vec4 v_texture_coordinate_2;
varying vec4 v_texture_coordinate_3;
varying vec4 v_texture_coordinate_4;

uniform sampler2D u_texture;
uniform vec4 u_texture_size;

const float mx = 0.325;
const float k = -0.250;
const float max_w = 0.25;
const float min_w =-0.05;
const float lum_add = 0.25;

void main()
{
  vec3 c00 = texture2D(u_texture, v_texture_coordinate_1.xy).rgb;
  vec3 c10 = texture2D(u_texture, v_texture_coordinate_1.zw).rgb;
  vec3 c20 = texture2D(u_texture, v_texture_coordinate_2.xy).rgb;
  vec3 c01 = texture2D(u_texture, v_texture_coordinate_4.zw).rgb;
  vec3 c11 = texture2D(u_texture, v_texture_coordinate.xy).rgb;
  vec3 c21 = texture2D(u_texture, v_texture_coordinate_2.zw).rgb;
  vec3 c02 = texture2D(u_texture, v_texture_coordinate_4.xy).rgb;
  vec3 c12 = texture2D(u_texture, v_texture_coordinate_3.zw).rgb;
  vec3 c22 = texture2D(u_texture, v_texture_coordinate_3.xy).rgb;
  
  vec3 dt = vec3(1.0, 1.0, 1.0);
  float md1 = dot(abs(c00 - c22), dt);
  float md2 = dot(abs(c02 - c20), dt);
  float w1 = dot(abs(c22 - c11), dt) * md2;
  float w2 = dot(abs(c02 - c11), dt) * md1;
  float w3 = dot(abs(c00 - c11), dt) * md2;
  float w4 = dot(abs(c20 - c11), dt) * md1;
  float t1 = w1 + w3;
  float t2 = w2 + w4;
  float ww = max(t1, t2) + 0.001;
  c11 = (w1 * c00 + w2 * c20 + w3 * c22 + w4 * c02 + ww * c11) / (t1 + t2 + ww);
  float lc1 = k / (0.12 * dot(c10 + c12 + c11, dt) + lum_add);
  float lc2 = k / (0.12 * dot(c01 + c21 + c11, dt) + lum_add);
  w1 = clamp(lc1 * dot(abs(c11 - c10), dt) + mx, min_w, max_w);
  w2 = clamp(lc2 * dot(abs(c11 - c21), dt) + mx, min_w, max_w);
  w3 = clamp(lc1 * dot(abs(c11 - c12), dt) + mx, min_w, max_w);
  w4 = clamp(lc2 * dot(abs(c11 - c01), dt) + mx, min_w, max_w);
  gl_FragColor.rgb = w1 * c10 + w2 * c21 + w3 * c12 + w4 * c01 + (1.0 - w1 - w2 - w3 - w4) * c11;
  gl_FragColor.a = 1.0;
}
</fragment>